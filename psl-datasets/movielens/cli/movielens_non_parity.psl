// Similarities like Pearson, Cosine, and Adjusted Cosine Similarity between items.
1.0 : rated(U,I1) & rated(U,I2) & rating(U,I1) & sim_items(I1,I2) >> rating(U,I2)^2

// Similarities like Pearson and Cosine Similarity between users.
1.0 : rated(U1,I) & rated(U2,I) & rating(U1,I) & sim_users(U1,U2) >> rating(U2,I)^2

// Predictions by other methods like SGD, Item based Pearson methods, and BPMF methods.

// Average prior of user rating and item ratings.
1.0 : rated(U,I) & avg_user_rating(U) >> rating(U,I)^2
1.0 : rated(U,I) & rating(U,I) >> avg_user_rating(U)^2
1.0 : rated(U,I) & avg_item_rating(I) >> rating(U,I)^2
1.0 : rated(U,I) & rating(U,I) >> avg_item_rating(I)^2

// Content rule by similarity.
1.0 : rated(U,I1) & rated(U,I2) & rating(U,I1) & sim_content_items(I1,I2) >> rating(U,I2)^2

// Non-Parity Unfairness Constraints
// Non parity minimizes the absolute difference between the overall average
// predicted ratings between the groups
rating(+U, I)/|U| = group_avg_item_rating(G, I) . {U: group_member(U, G) & target(U, I)}
group_avg_item_rating(G, +I)/|I| = group_avg_rating(G) . {I: group_item_block(G, I)}
1.0 : group_avg_rating(G1) = group_avg_rating(G2)^2